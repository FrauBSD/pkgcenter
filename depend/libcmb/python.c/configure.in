# $FrauBSD: pkgcenter/depend/libcmb/python.c/configure.in 2019-04-10 07:22:17 -0700 freebsdfrau $

AC_INIT
############################################################ CONFIGURATION

#
# AC_PREREQ (VERSION)
# 	Minimum version of autoconf required to generate `configure'.
#
AC_PREREQ(2.69)

#
# AC_REVISION (REVISION-INFO)
# 	Place static version identifier information into autoconf-generated
# 	`configure' for source-code tracking purposes.
#
AC_REVISION($Revision: 1.1 $)

############################################################ OPTIONS

AC_ARG_WITH(python,
	AS_HELP_STRING([--with-python=CMD], [Destination python interpreter]))
AS_IF(path=`which ${with_python:-python}`, [PYTHON="$path"], [PYTHON=python])

############################################################ FUNCTIONS

m4_define([PYTHON_CFG], [`$1 -c 'from distutils.sysconfig import *; \
	print(get_config_var("$2"))' | sed -e 's/None//'`])
m4_define([PYTHON_VER], [`$1 -c 'from distutils.sysconfig import *; \
	print(get_python_version())'`])
m4_define([CONCAT], ["$1$2"])
m4_define([CONCAT_UNQUOTED], [$1$2])

############################################################ DEFINES

case "PYTHON_CFG([$PYTHON], [VERSION])" in
2|2.*)
	AC_SUBST(EXT_SUFFIX, PYTHON_CFG([$PYTHON], [SO]))
	;;
*) # 3 or higher
	AC_SUBST(EXT_SUFFIX, PYTHON_CFG([$PYTHON], [EXT_SUFFIX]))
esac
AC_SUBST(INCLUDEPY, PYTHON_CFG([$PYTHON], [INCLUDEPY]))
AC_SUBST(CCSHARED,  PYTHON_CFG([$PYTHON], [CCSHARED]))
AC_SUBST(CFLAGS,    PYTHON_CFG([$PYTHON], [CFLAGS]))
AC_SUBST(LDSHARED,  PYTHON_CFG([$PYTHON], [LDSHARED]))
case "`uname`" in
Darwin)
  case "`sw_vers -productVersion`" in
  10.[6-9]|10.[6-9].*|10.1[0-3]|10.1[0-3].*)
    AC_SUBST(LIBDEST, CONCAT(/Library/Python/, PYTHON_VER([$PYTHON])))
    ;;
  *) # 10.14 or higher
    AC_SUBST(LIBDEST, CONCAT(~/Library/Python/,
      CONCAT_UNQUOTED(PYTHON_VER([$PYTHON]), /lib/python)))
  esac
  LDSHARED=`echo "$LDSHARED" | sed -e 's/\(-arch x86_64\) -arch i386 /\1 /'`
  ;;
*)
  AC_SUBST(LIBDEST, PYTHON_CFG([$PYTHON], [LIBDEST]))
esac

############################################################ OUTPUTS

#
# AC_CONFIG_FILES (FILE ..., [COMMANDS], [INIT-CMDS])
# 	Files to instantiate.
#
AC_CONFIG_FILES(Makefile cmb/Makefile)

#
# Generate/launch `config.status' (which in-turn generates makefiles, etc.).
#
AC_OUTPUT

################################################################################
# END
################################################################################
